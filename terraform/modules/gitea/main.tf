terraform {
  required_providers {
    ansible = {
      source  = "ansible/ansible"
      version = "1.5.0"
    }
    pihole = {
      source  = "lukaspustina/pihole"
      version = "0.3.1"
    }
    proxmox = {
      source  = "telmate/proxmox"
      version = "3.0.2-rc09"
    }
  }
}

resource "proxmox_vm_qemu" "gitea" {
  name               = "gitea"
  tags               = "tf"
  target_node        = var.proxmox_host
  clone              = var.cloud_init_template_name
  full_clone         = true
  power_state        = "running"
  start_at_node_boot = true
  agent              = 1
  os_type            = "cloud-init"
  memory             = var.memory
  scsihw             = "virtio-scsi-pci"
  bootdisk           = "scsi0"
  vmid               = 301

  cpu {
    cores = var.cores
  }

  disks {
    ide {
      ide2 {
        cloudinit {
          storage = var.proxmox_storage
        }
      }
    }
    scsi {
      scsi0 {
        disk {
          size    = var.disk_size
          storage = var.proxmox_storage
          format  = "qcow2"
        }
      }
    }
  }

  network {
    id     = 0
    model  = "virtio"
    bridge = "vmbr0"
  }

  lifecycle {
    ignore_changes = [
      qemu_os,
      startup_shutdown,
    ]
  }

  # cloud-init

  ipconfig0 = "ip=${var.ip}/24,gw=${var.network_gateway}"

  ciuser  = "ops"
  sshkeys = var.ssh_keys
}

resource "proxmox_vm_qemu" "gitea_runner" {
  name               = "gitea-runner"
  tags               = "tf"
  target_node        = var.proxmox_host
  clone              = var.cloud_init_template_name
  full_clone         = true
  power_state        = "running"
  start_at_node_boot = true
  agent              = 1
  os_type            = "cloud-init"
  memory             = var.runner_memory
  scsihw             = "virtio-scsi-pci"
  bootdisk           = "scsi0"
  vmid               = 306

  cpu {
    cores = var.runner_cores
  }

  disks {
    ide {
      ide2 {
        cloudinit {
          storage = var.proxmox_storage
        }
      }
    }
    scsi {
      scsi0 {
        disk {
          size    = var.runner_disk_size
          storage = var.proxmox_storage
          format  = "qcow2"
        }
      }
    }
  }

  network {
    id     = 0
    model  = "virtio"
    bridge = "vmbr0"
  }

  lifecycle {
    ignore_changes = [
      qemu_os,
      startup_shutdown,
    ]
  }

  # cloud-init

  ipconfig0 = "ip=${var.runner_ip}/24,gw=${var.network_gateway}"
  nameserver = var.nameserver

  ciuser  = "ops"
  sshkeys = var.ssh_keys
}

locals {
  fqdn        = "git.${var.internal_dns_zone}"
  runner_fqdn = "gitea-runner.${var.internal_dns_zone}"
}

resource "ansible_host" "git" {
  name   = local.fqdn
  groups = ["git"]
  variables = {
    ansible_host = proxmox_vm_qemu.gitea.ssh_host
    ansible_user = "ops"
  }
}

resource "ansible_host" "gitea_runner" {
  name   = local.runner_fqdn
  groups = ["gitea_runner"]
  variables = {
    ansible_host = proxmox_vm_qemu.gitea_runner.ssh_host
    ansible_user = "ops"
  }
}

resource "pihole_dns_record" "git" {
  domain = local.fqdn
  ip     = proxmox_vm_qemu.gitea.ssh_host
}

resource "pihole_dns_record" "gitea_runner" {
  domain = local.runner_fqdn
  ip     = proxmox_vm_qemu.gitea_runner.ssh_host
}
