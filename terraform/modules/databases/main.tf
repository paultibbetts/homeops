terraform {
  required_providers {
    ansible = {
      source  = "ansible/ansible"
      version = "1.5.0"
    }
    pihole = {
      source  = "lukaspustina/pihole"
      version = "0.3.1"
    }
    proxmox = {
      source  = "telmate/proxmox"
      version = "3.0.2-rc09"
    }
  }
}

resource "proxmox_vm_qemu" "mysql" {
  name               = "mysql"
  tags               = "database;tf"
  target_node        = var.proxmox_host
  clone              = var.cloud_init_template_name
  full_clone         = true
  power_state        = "running"
  start_at_node_boot = true
  agent              = 1
  os_type            = "cloud-init"
  memory             = var.mysql_memory
  scsihw             = "virtio-scsi-pci"
  vmid               = 201
  hastate            = "ignored"

  cpu {
    cores = var.mysql_cores
  }

  disks {
    ide {
      ide2 {
        cloudinit {
          storage = var.proxmox_storage
        }
      }
    }
    scsi {
      scsi0 {
        disk {
          size    = var.mysql_disk_size
          storage = var.proxmox_storage
          format  = "qcow2"
        }
      }
    }
  }

  network {
    id     = 0
    model  = "virtio"
    bridge = "vmbr0"
  }

  lifecycle {
    ignore_changes = [
      qemu_os,
      startup_shutdown,
    ]
  }

  # cloud-init

  ipconfig0 = "ip=${var.mysql_ip}/24,gw=${var.network_gateway}"

  ciuser  = "ops"
  sshkeys = var.ssh_keys
}

resource "proxmox_vm_qemu" "postgres" {
  name               = "postgres"
  tags               = "database;tf"
  target_node        = var.proxmox_host
  clone              = var.cloud_init_template_name
  full_clone         = true
  power_state        = "running"
  start_at_node_boot = true
  agent              = 1
  os_type            = "cloud-init"
  memory             = var.postgres_memory
  scsihw             = "virtio-scsi-pci"
  bootdisk           = "scsi0"
  vmid               = 202

  cpu {
    cores = var.postgres_cores
  }

  disks {
    ide {
      ide2 {
        cloudinit {
          storage = var.proxmox_storage
        }
      }
    }
    scsi {
      scsi0 {
        disk {
          size    = var.postgres_disk_size
          storage = var.proxmox_storage
          format  = "qcow2"
        }
      }
    }
  }

  network {
    id     = 0
    model  = "virtio"
    bridge = "vmbr0"
  }

  lifecycle {
    ignore_changes = [
      qemu_os,
      startup_shutdown,
    ]
  }

  # cloud-init

  ipconfig0 = "ip=${var.postgres_ip}/24,gw=${var.network_gateway}"

  ciuser  = "ops"
  sshkeys = var.ssh_keys
}

locals {
  mysql = {
    fqdn = "mysql.${var.internal_dns_zone}"
  }
  postgres = {
    fqdn = "postgres.${var.internal_dns_zone}"
  }
}

resource "ansible_host" "mysql" {
  name   = local.mysql.fqdn
  groups = ["database", "mysql"]
  variables = {
    ansible_host = proxmox_vm_qemu.mysql.ssh_host
    ansible_user = "ops"
  }
}

resource "pihole_dns_record" "mysql" {
  domain = local.mysql.fqdn
  ip     = proxmox_vm_qemu.mysql.ssh_host
}

resource "ansible_host" "postgres" {
  name   = local.postgres.fqdn
  groups = ["database", "postgres"]
  variables = {
    ansible_host = proxmox_vm_qemu.postgres.ssh_host
    ansible_user = "ops"
  }
}

resource "pihole_dns_record" "postgres" {
  domain = local.postgres.fqdn
  ip     = proxmox_vm_qemu.postgres.ssh_host
}
